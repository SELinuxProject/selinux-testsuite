
## <summary>test_policy policy</summary>
## <desc>
##      <p>
##              More descriptive text about test_policy.  The <desc>
##              tag can also use <p>, <ul>, and <ol>
##              html tags for formatting.
##      </p>
##      <p>
##              This policy supports the following test_policy features:
##              <ul>
##              <li>Feature A</li>
##              <li>Feature B</li>
##              <li>Feature C</li>
##              </ul>
##      </p>
## </desc>
#

interface(`testsuite_domain_type_common',`
	gen_require(`
		attribute testsuite_domain;
	')

	typeattribute $1 testsuite_domain;
')

interface(`testsuite_domain_type',`
	domain_type($1)
	testsuite_domain_type_common($1)

	# task_create and task_setpgid need to exclude these permissions
	# so put them here instead of testsuite_domain_type_common()
	allow $1 self:process { fork setpgid };
')

interface(`testsuite_domain_type_minimal',`
	gen_require(`
		type setrans_var_run_t;
	')

	testsuite_domain_type_common($1)

	# minimal set of rules to substitute missing domain_type()
	allow $1 proc_t:dir { search };
	allow $1 proc_t:lnk_file { read };
	allow $1 self:dir { search };
	allow $1 self:file { open read write };
	dontaudit $1 security_t:filesystem getattr;
	dontaudit $1 self:file getattr;
	dontaudit $1 setrans_var_run_t:dir search;

	ifdef(`anaconda_fd_use',`
		anaconda_fd_use($1) # for rpm-ostree --apply-live magic
	')
')

interface(`testsuite_caller_association_setcontext',`
	gen_require(`
		attribute testsuite_caller_ifconfig_domain;
	')

	allow testsuite_caller_ifconfig_domain $1:association setcontext;
')

interface(`testsuite_caller_packet_relabelto',`
	gen_require(`
		attribute testsuite_caller_iptables_domain;
	')

	allow testsuite_caller_iptables_domain $1:packet relabelto;
')

# Workarounds for refpolicy:

ifdef(`dev_rw_infiniband_dev', `', ` dnl
interface(`dev_rw_infiniband_dev', `
    dev_rw_generic_blk_files($1)
    dev_rw_generic_chr_files($1)
')
')

ifdef(`mount_rw_pid_files', `', ` dnl
interface(`mount_rw_pid_files', `
    mount_rw_runtime_files($1)
')
')

# If the macro is not defined, then most probably module_request permission
# is just not supported (and relevant operations should be just allowed).
ifdef(`kernel_request_load_module', `', ` dnl
interface(`kernel_request_load_module', `')
')
